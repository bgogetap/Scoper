plugins {
    id "com.jfrog.bintray" version "1.6"
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

group = PROJECT_GROUP_ID
version = PROJECT_VERSION

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

configurations {
    lintChecks
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:support-annotations:$supportLibraryVersion"

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$mockitoVersion"

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    lintChecks project(path: ':scoper-lint', configuration: 'lintChecks')
}

task copyLintJar(type: Copy) {
    from(configurations.lintChecks) {
        rename { 'lint.jar' }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Scoper'
            description PROJECT_DESCRIPTION
            url PROJECT_VCS_URL

            packaging 'aar'
            groupId PROJECT_GROUP_ID
            artifactId 'scoper'
            version PROJECT_VERSION
        }
    }
}

bintray {
    Properties properties = new Properties()
    if (file('local.properties').exists()) {
        properties.load(new FileInputStream(file('local.properties')))
    }
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'Scoper'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = PROJECT_VCS_URL
        version {
            name = PROJECT_VERSION
            desc = PROJECT_DESCRIPTION
        }
    }
}
